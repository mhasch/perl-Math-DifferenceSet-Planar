#!/usr/bin/env perl

# Copyright (c) 2019-2023 Martin Becker, Blaubeuren.
# This package is free software; you can distribute it and/or modify it
# under the terms of the Artistic License 2.0 (see LICENSE file).

# Get text representation of difference set samples from old database
# (database versions < 3.0, Math-DifferenceSet-Planar versions < 1.000).

# For current database versions, use the pds_iterate example script.

use strict;
use warnings;
use DBI qw(SQL_INTEGER SQL_BLOB);
use DBD::SQLite::Constants qw(SQLITE_OPEN_READONLY);

my $DB_TYPE = 'dbi:SQLite';
my $DB_CONF = {
    RaiseError        => 1,
    AutoCommit        => 1,
    sqlite_open_flags => SQLITE_OPEN_READONLY,
};
my $NUM_RE  = qr/(0|[1-9][0-9]*|)/;
my ($DB_NAME, $lo_order, $hi_order) = process_options();

my $TABLE   = 'difference_set';
my $COLUMNS = q[modulus, deltas];
my $KEY     = q[order_];
my $WHERE   = _where_clause($KEY, $lo_order, $hi_order);

my $dbh = DBI->connect("$DB_TYPE:dbname=$DB_NAME", q[], q[], $DB_CONF);
my $sth = $dbh->prepare(qq[SELECT $COLUMNS FROM $TABLE $WHERE ORDER BY $KEY]);
$sth->execute;
while (my ($modulus, $deltas) = $sth->fetchrow_array) {
    my $sum = 0;
    my @elements = map { $sum += $_ } 0, 1, unpack 'w*', $deltas;
    print "@elements (mod $modulus)\n";
}
$sth->finish;
$dbh->disconnect;

sub process_options {
    my $USAGE = "usage: dump_pds [-f dbname] [-r [ord_min]-[ord_max]]\n";
    my $db_name = 'pds.db';
    my @range   = (q[], q[]);
    while (@ARGV && $ARGV[0] =~ /^-(.+)/s) {
        my $opt = $1;
        shift @ARGV;
        last if $opt eq q[-];
        $db_name =           $1, next if $opt =~ /^f(.+)\z/s;
        $db_name = shift(@ARGV), next if $opt eq 'f' && @ARGV;
        @range   =     ($1, $2), next if $opt =~ /^r$NUM_RE-$NUM_RE\z/;
        @range   =     ($1, $2), shift(@ARGV), next
            if $opt eq 'r' && @ARGV && $ARGV[0] =~ /^$NUM_RE-$NUM_RE\z/;
        die $USAGE;
    }
    if (!-e $db_name) {
        die "$db_name: nonexistent\n";
    }
    return ($db_name, @range);
}

sub _where_clause {
    my ($key, $lo_order, $hi_order) = @_;
    my @selection = ();
    if ($lo_order ne q[]) {
        if ($hi_order ne q[]) {
            push @selection, qq[$key BETWEEN '$lo_order' AND '$hi_order'];
        }
        else {
            push @selection, qq[$key >= '$lo_order'];
        }
    }
    elsif ($hi_order ne q[]) {
        push @selection, qq[$key <= '$hi_order'];
    }
    return @selection? q[WHERE ] . join(q[ AND ], @selection): q[];
}
